#!/bin/zsh

##############################################################################
##
## pmw-stream-feed
## start a liquidsoap process connected to a running instance of JACK
## arguments: Stream-URL Source-client-password
##
##############################################################################

# Log STDOUT and STDERR to separate files.
exec 1> /var/tmp/${0##*/}.out
exec 2> /var/tmp/${0##*/}.err

AWK=$(whence awk) ; AWK=${AWK:-${ROOT:-/}bin/awk}
CUT=$(whence cut) ; CUT=${CUT:-${ROOT:-/}usr/bin/cut}
GETOPT=$(whence getopt) ; GETOPT=${GETOPT:-${ROOT:-/}usr/bin/getopt}
GREP=$(whence grep) ; GREP=${GREP:-${ROOT:-/}bin/grep}
JACK_CONNECT=$(whence jack_connect) ; JACK_CONNECT=${JACK_CONNECT:-${ROOT:-/}usr/bin/jack_connect}
JACK_LSP=$(whence jack_lsp) ; JACK_LSP=${JACK_LSP:-${ROOT:-/}usr/bin/jack_lsp}
LIQUIDSOAP=$(whence liquidsoap) ; LIQUIDSOAP=${LIQUIDSOAP:-${ROOT:-/}usr/bin/liquidsoap}
LOGGER=$(whence logger) ; LOGGER=${LOGGER:-${ROOT:-/}usr/bin/logger}
SORT=$(whence sort) ; SORT=${SORT:-${ROOT:-/}usr/bin/sort}

##
## the stream URL should be of the form
## http://source:<PASSWORD>@<HOSTNAME-OR-IP-ADDRESS>:<PORT>/<MOUNTPOINT>
##
streamURL=${1:?"What stream URL?"}

streamScript=${STREAM_SCRIPT:-${ROOT:-/}usr/local/etc/pmw/pmw-stream-feed.liq}

## Get zsh functions necessary for this script.
[[ -r ${ROOT:-/}usr/local/bin/zsh-functions ]] && source ${ROOT:-/}usr/local/bin/zsh-functions

sleepDuration=${SLEEP_DURATION:-5}
maxAttempts=${MAX_ATTEMPTS:-5}

## parse the pieces out of the stream URL
streamProtocol=${streamURL%%:*}
streamUser=${${streamURL#*://}%%:*}
streamPassword=${${streamURL#*://*:}%%@*}
streamHost=${${streamURL#*@}%%:*}
streamPort=${${streamURL##*:}%/*}
streamMountPoint=${streamURL##*/}

## The default Rivendell production playback port (we tack on the "L"
## and "R" when we connect the ports below).
rivendellPlaybackPort=${2:-"rivendell_0:playout_0"}

if [[ -r ${streamScript} ]] ; then

    ## snag the encoder jack client name from the script
    ## BUG ALERT: this completely depends on the formatting of the "input.jack" lines in that script!
    ## default to the PMW standard: "encode-in"
    encoderID=$(fgrep -A 1 "input.jack" ${streamScript} | tail -n 1 | cut -d'"' -f2)
    encoderID=${encoderID:-"encode-in"}

    if ${LIQUIDSOAP} --daemon ${streamScript} -- ${streamProtocol} ${streamHost} ${streamPort} ${streamMountPoint} "${streamPassword}" ; then
	${LOGGER} -t ${0##*/} -p local7.info -i "INFO: Started ${streamScript} in liquidsoap."

	count=1
	while (( count <= maxAttempts )) ; do
	    if ${JACK_LSP} | ${GREP} -q encode-in ; then
		if ${JACK_LSP} | ${GREP} -q ${rivendellPlaybackPort} ; then
		    ${JACK_CONNECT} ${encoderID}:in_0 ${rivendellPlaybackPort}L
		    ${JACK_CONNECT} ${encoderID}:in_1 ${rivendellPlaybackPort}R
		    ${LOGGER} -t ${0##*/} -p local7.info -i "INFO: Connected liquidsoap ports to ${rivendellPlaybackPort}."
		    break
		fi
	    fi
	    (( count++ ))
	    sleep ${sleepDuration}
	done

	if (( count >= maxAttempts )) ; then
	    ${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Cannot see liquidsoap ports. Liquidsoap appears to be not running."
	fi
    else
	${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: unable to properly start liquidsoap. Check the configuration and log files."
    fi
else
    ${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Cannot read liquidsoap script ${streamScript}."
fi

exit
