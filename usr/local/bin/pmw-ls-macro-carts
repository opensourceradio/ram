#!/bin/zsh
# shellcheck shell=bash disable=SC2016,SC2034,SC2128,SC2154,SC2179,SC2190,SC2207,SC2296,SC2299,SC2312
# SC2016: $Hash is not a variable, it's a token that gets replaced by git(1)
# SC2034: shellcheck does not know about ZSH param expansions.
# SC2128 (warning): Expanding an array without an index only gives the first element. (ZSH)
# SC2154: the only "unassigned" parameters *should be* upper case command names.
# SC2179 (warning): Use array+=("item") to append items to an array. (ZSH)
# SC2190: ZSH associative arrays work differently from Bash associative arrays.
# SC2207: mapfile or read -a are Bash-isms.
# SC2296: ZSH params CAN start with '('.
# SC2299: ZSH params CAN be nested.
# SC2312: ignore 'invoke separately' suggestions.

##############################################################################
#
# List macro carts: select the CART number, TITLE and MACROS (and optionally
# NOTES) from Rivendell.CART.
#
##############################################################################

# This script complies with Semantic Versioning: http://semver.org/
declare -ri v_major=0
declare -ri v_minor=2
declare -ri v_patch=1
declare -r v_hash='$Hash$'

##########################################################################################
##########################################################################################
##
## Locally defined subroutines
##
##########################################################################################
##########################################################################################

# Show how to use this command.
function usage() {
  local -r my_name="${1:t}"

  ${CAT} <<EOF
NAME

${my_name}

SYNOPSIS

${my_name} [ --long (-l) ] [ --notes (-n) ]
                   [ --group (-g) <Group_Name[,...]> ]
                   [ --pretty (-p) ]
                   [ --help (-h) ] [ --verbose (-v) ] [ --version (-V) ]

DESCRIPTION

$(${FMT} <<<"${my_name} lists Rivendell Macro Carts. It also optionally lists details about those Macro Carts.")

$(${FMT} <<<"Specify '--long' (short option: '-l') to have ${my_name} show the details of the Macro Cart.")

$(${FMT} <<<"Specify '--group <Group_Name[,...]>' (short option: '-g') to have ${my_name} show Macro Carts in the Group(s) specified. Separate multiple Group names with ',' (comma).")

$(${FMT} <<<"Specify '--notes' (short option: '-n') to have ${my_name} show the Notes associated with the Macro Cart.")

$(${FMT} <<<"Specify '--pretty' (short option: '-p') to have ${my_name} 'pretty-print' the contents of the Macro Cart.")

$(${FMT} <<<"Specify '--verbose' (short option: '-v') to have ${my_name} be 'chatty' about its progress.")

$(${FMT} <<<"Specify '--version' (short option: '-V') to have ${my_name} display its version number and exit.")

EOF
}

##########################################################################################
##########################################################################################
##
## Script Main Line
##
##########################################################################################
##########################################################################################

# Prefer the instance in /usr/bin (that gets installed with the
# rdfetch package) over /usr/local/bin
if [[ -f /usr/bin/zsh-functions ]] ; then
    source /usr/bin/zsh-functions
elif [[ -f /usr/local/bin/zsh-functions ]] ; then
    source /usr/local/bin/zsh-functions
else
    print "Missing zsh-functions library. Cannot continue." >&2
    exit 1
fi

############# BEGIN external shell commands used in this script. #############
# This script uses these 4 external commands.
# Look for them in their upper case, parameter expanded form.
our_commands=( cat fmt getopt perl )
# Find the executables we need; this uses some basic shell and a ZSH trick:
# the (U) in the eval says to evaluate the parameter as all upper case
# letters. This snippet generates shell parameters representing the upper case
# equivalent of the command names and sets the parameter values to the full path
# of the commands.
# Refresh this segment in Emacs by marking the appropriate region (or the whole
# buffer with C-xH) and replacing it with C-uM-|mk-ourCommands --script (shell-command-on-region).
typeset C D
# SC2048: shellcheck overly aggressive quote recommendation.
# shellcheck disable=SC2048
for C in ${our_commands[*]} ; do
  # shellcheck disable=SC2154 # ZSH: ${path} is set by the shell.
  for D in ${path} ; do
    # shellcheck disable=SC2140,SC2086,SC2296 # we need the quotes, ZSH-specific expansion
    [[ -x "${D}/${C}" ]] && { eval "${(U)C//-/_}"="${D}/${C}" ; break ; }
  done
  # shellcheck disable=SC2296,SC2312 # ZSH-specific expansion
  [[ -x $(eval print \$"${(U)C//-/_}") ]] || { print "Cannot find ${C}! Done."; return 1 ; }
done
unset our_commands C D
############## END external shell commands used in this script. ##############

# Command line options.
typeset GROUP
typeset -i LONG=0
typeset -i NOTES=0
typeset -i PRETTY=0

# With the "-l (--long)" option, display the contents of the macro as
# well as the number and title.
# With the "-n (--notes)" option, display the cart notes (if any)
# as well as the other stuff.
if ! TEMP=$(${GETOPT} -o hg:lnpVv --long help,group:,long,notes,pretty,version,verbose -n "${0:t}" -- "${@}") ; then
  print "getopt(1) Fatal Error (${?}). Terminating..." >&2
  return 1
fi
eval set -- "${TEMP}"
while : ; do
  # VERBOSE is used, but in a non-Bash way.
  # shellcheck disable=SC2034
  case "${1}" in
    -h|--help) usage "${0}" ; exit ;;
    -g|--grou*) GROUP="${2}" ; shift 2 ;;
    -l|--long) LONG=1 ; shift ;;
    -n|--note*) NOTES=1 ; shift ;;
    -p|--pretty) PRETTY=1 ; shift ;;
    -V|--version) SHOW_VERSION=1 ; shift ;;
    -v|--verbose) ((VERBOSE += 1)) ; shift ;;
    --) shift ; break ;;
    *) print "${0:t}: getopt internal error!. Terminating..." >&2 ; return 1 ;;
  esac
done
unset TEMP

if ((SHOW_VERSION)) ; then
    typeset commit_hash="${v_hash}"
    [[ "${v_hash}" == '$Hash$' ]] &&
	commit_hash="prerelase"
    print "${0:t}: version ${v_major}.${v_minor}.${v_patch}-${${commit_hash#\$Hash: }%$}"
    exit 0
fi

# Accept a single cart number on the command line (after options) to display
# that CART.
condition=${1:+" and NUMBER = ${1}"}

typeset group_list
if [[ -n "${GROUP}" ]] ; then
  group_list="'${(j:',':)${(s:,:)GROUP}[*]}'"
fi

# SC2178 (warning): Variable was used as an array but is now assigned a string. (ZSH)
# shellcheck disable=SC2178
typeset query="select concat("
[[ -n "${GROUP}" ]] && query+="GROUP_NAME, ': ', "
query+="NUMBER, ': ', TITLE, '^_^'"
((LONG)) && query+=", MACROS"
((NOTES)) && query+=", replace(NOTES, '\n', ' ')"
query+=")"
query+=" from CART "
query+="where TYPE = 2 "
[[ -n "${GROUP}" ]] && query+="and GROUP_NAME in ( ${group_list} ) "
query+="${condition} "
query+="order by "
[[ -n "${GROUP}" ]] && query+="GROUP_NAME, "
query+="NUMBER"

# Macros are stored in the database with TYPE set to 2.
# Run the query through sed(1) to make multi-line NOTES more visually appealing.
if ((PRETTY && LONG)) ; then
  doSQL "${query}" | ${PERL} -ne 's{!}{!\n  }g;' -e 's{\^_\^}{\n  }g; print'
elif ((LONG)) ; then
  doSQL "${query}" | ${PERL} -ne 's{!}{!  }g;' -e 's{\^_\^}{ }g; print'
else
  doSQL "${query}" | ${PERL} -ne 's{\^_\^}{}g; print'
fi

exit

# Local Variables: ***
# mode:shell-script ***
# indent-tabs-mode: f ***
# sh-indentation: 2 ***
# sh-basic-offset: 2 ***
# sh-indent-for-do: 0 ***
# sh-indent-after-do: + ***
# sh-indent-comment: t ***
# sh-indent-after-case: + ***
# sh-indent-after-done: 0 ***
# sh-indent-after-else: + ***
# sh-indent-after-if: + ***
# sh-indent-after-loop-construct: + ***
# sh-indent-after-open: + ***
# sh-indent-after-switch: + ***
# sh-indent-for-case-alt: ++ ***
# sh-indent-for-case-label: + ***
# sh-indent-for-continuation: + ***
# sh-indent-for-done: 0 ***
# sh-indent-for-else: 0 ***
# sh-indent-for-fi: 0 ***
# sh-indent-for-then: 0 ***
# End: ***
